//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
plugins {
  id 'java'
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version '1.9.0'
  id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

ext {
  mainVersion = '0.0.1'
  devMode = 'alpha'
}
group 'nju.eur3ka.bmo'
version = mainVersion + '-' + devMode

distributions {
  main {
    contents {
      from file('README.md')
      file("assets").listFiles().each {
        from(it) {
          into "assets"
        }
      }
      from(file("jre")) { into "jre"}
    }
  }
}

repositories {
  mavenCentral()
}

application {
  mainClass = 'nju.eur3ka.bmo.MainKt'
}
kotlin {
  jvmToolchain( 17 )
}
javafx {
  version = '17.0.6'
  modules = ['javafx.controls']
}
dependencies {
  testImplementation "org.jetbrains.kotlin:kotlin-test"
  testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
  // 日志
  implementation "org.slf4j:slf4j-api:1.7.36"
  implementation "ch.qos.logback:logback-core:1.2.11"
  implementation "ch.qos.logback:logback-classic:1.2.11"
  // ElementFX-1.3
  implementation files("lib/ElementFX-1.3-SNAPSHOT.jar")
  // JavaFX图标库
  implementation "org.kordamp.ikonli:ikonli-javafx:12.3.1"
  implementation "org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1"
  // Json序列化
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"
  // 压缩包处理库
  implementation "net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01"
  implementation "net.sf.sevenzipjbinding:sevenzipjbinding-all-platforms:16.02-2.01"
}

test {
  useJUnitPlatform()
}

// 修改启动脚本
tasks.startScripts {
  doLast {
    // 删除unix启动脚本
    unixScript.delete()
    def windowsScriptFile = getWindowsScript()
    // 向启动脚本中注入指定的java.exe路径，并强制跳转到执行命令
    def startCmdLine ="@rem Set Forced Java.exe and start\nset JAVA_EXE=%APP_HOME%\\jre\\bin\\java.exe goto execute\n\n@rem Find java.exe"
    windowsScriptFile.text = windowsScript.text.replace("@rem Find java.exe", startCmdLine)
    // 启动脚本中添加pause指令
    windowsScriptFile.text = windowsScript.text.replace("\n:end", "pause\n\n:end")
  }
}


jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'app'
  }
}

jlinkZip {
  group = 'distribution'
}